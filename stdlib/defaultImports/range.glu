import operators::*;

public struct Range {
    start: Int,
    end: Int,
    inclusive: Bool
}

public struct RangeIterator {
    current: Int
}

@inline func makeRange(start: Int, end: Int, inclusive: Bool) -> Range {
    return { start, end, inclusive };
}

@inline func makeIterator(value: Int) -> RangeIterator {
    return { value };
}

@inline public func ..<(start: Int, end: Int) -> Range {
    return makeRange(start, end, false);
}

@inline public func ...(start: Int, end: Int) -> Range {
    return makeRange(start, end, true);
}

public func begin(range: Range) -> RangeIterator {
    return makeIterator(range.start);
}

public func end(range: Range) -> RangeIterator {
    return makeIterator(range.inclusive ? range.end + 1 : range.end);
}

public func next(iterator: RangeIterator) -> RangeIterator {
    return makeIterator(iterator.current + 1);
}

public func ==(lhs: RangeIterator, rhs: RangeIterator) -> Bool {
    return lhs.current == rhs.current;
}

public func !=(lhs: RangeIterator, rhs: RangeIterator) -> Bool {
    return !(lhs == rhs);
}

public func .*(iterator: RangeIterator) -> Int {
    return iterator.current;
}
